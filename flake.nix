{
  description = "CandyPi - Lightning-paid candy dispenser for Raspberry Pi";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    rust-overlay.url = "github:oxalica/rust-overlay";
    crane.url = "github:ipetkov/crane";
    flake-utils.url = "github:numtide/flake-utils";
  };

  outputs =
    {
      self,
      nixpkgs,
      rust-overlay,
      crane,
      flake-utils,
      ...
    }:
    flake-utils.lib.eachSystem [ "x86_64-linux" "aarch64-linux" ] (
      system:
      let
        overlays = [ (import rust-overlay) ];
        pkgs = import nixpkgs {
          inherit system overlays;
        };

        lib = nixpkgs.lib;

        rustToolchain = pkgs.rust-bin.stable.latest.default.override {
          extensions = [
            "rust-src"
            "rust-analyzer"
          ];
        };

        # Initialize crane with our rust toolchain
        craneLib = (crane.mkLib pkgs).overrideToolchain rustToolchain;

        # Filter source to only include Rust-related files
        src = lib.cleanSourceWith {
          src = ./.;
          filter =
            path: type:
            let
              baseName = baseNameOf path;
              relPath = lib.removePrefix (toString ./. + "/") (toString path);
            in
            # Include Cargo files
            baseName == "Cargo.toml"
            || baseName == "Cargo.lock"
            ||
              # Include Rust source files
              lib.hasSuffix ".rs" baseName
            ||
              # Include src directory
              (type == "directory" && (baseName == "src" || lib.hasPrefix "src/" relPath));
        };

        build_arch_underscores =
          lib.strings.replaceStrings [ "-" ] [ "_" ]
            pkgs.stdenv.buildPlatform.config;

        rocksdb = pkgs.rocksdb_8_11.override { enableLiburing = false; };

        # Common arguments for crane builds
        commonArgs = {
          inherit src;
          strictDeps = true;

          nativeBuildInputs = with pkgs; [
            pkg-config
            cmake
            clang
          ];

          buildInputs = [ rocksdb ];

          # Disable fortify to avoid GCC warnings-as-errors in aws-lc-sys
          hardeningDisable = [ "fortify" ];

          LIBCLANG_PATH = "${pkgs.llvmPackages.libclang.lib}/lib";

          "ROCKSDB_STATIC" = "true";
          "ROCKSDB_LIB_DIR" = "${rocksdb}/lib/";

          # Wrap CC to disable warnings that cause aws-lc-sys build to fail
          preBuild = ''
            export CC_WRAPPER="$NIX_BUILD_TOP/cc-wrapper"
            cat > "$CC_WRAPPER" << 'WRAPPER'
            #!/bin/sh
            exec ${pkgs.stdenv.cc}/bin/cc -Wno-error=stringop-overflow -Wno-error=array-bounds -Wno-error=restrict "$@"
            WRAPPER
            chmod +x "$CC_WRAPPER"
            export CC="$CC_WRAPPER"
          '';
        };

        # Stage 1: Build only the dependencies (cached separately)
        cargoArtifacts = craneLib.buildDepsOnly commonArgs;

        # Stage 2: Build the actual application using cached dependencies
        blitzidPackage = craneLib.buildPackage (
          commonArgs
          // {
            inherit cargoArtifacts;

            cargoExtraArgs = "--bin blitzid";

            meta = with lib; {
              description = "Blitzi Lightning REST API daemon";
              homepage = "https://github.com/elsirion/blitzi";
              license = licenses.mit;
              maintainers = [ ];
            };
          }
        );

        blitzid-image = pkgs.dockerTools.buildLayeredImage {
          name = "blitzid";
          contents = [
            blitzidPackage
            pkgs.bash
            pkgs.coreutils
            pkgs.curl
          ]
          ++ pkgs.lib.optionals pkgs.stdenv.isLinux [ pkgs.busybox ];

          config = {
            Cmd = [
              "${blitzidPackage}/bin/blitzid"
            ];
            Env = [
              "BLITZID_HOST=0.0.0.0"
              "BLITZID_DATADIR=/data"
            ];
            Expose = [ "3000" ];
            Volumes = {
              "/data" = { };
            };
          };
        };
      in
      {
        packages = {
          default = blitzidPackage;
          blitzid = blitzidPackage;
          inherit blitzid-image;
        };

        devShells.default = pkgs.mkShell {
          nativeBuildInputs = with pkgs; [
            stdenv.cc # Include nix cc wrapper which respects NIX_CFLAGS_COMPILE
          ];

          buildInputs = with pkgs; [
            rustToolchain
            rust-bin.nightly.latest.rustfmt
            pkg-config
            cmake
            clang
            llvmPackages.libclang
            llvmPackages.libcxxClang
          ];

          LIBCLANG_PATH = "${pkgs.llvmPackages.libclang.lib}/lib";
          RUSTFMT = "${pkgs.rust-bin.nightly.latest.rustfmt}/bin/rustfmt";
          "ROCKSDB_STATIC" = "true";
          "ROCKSDB_LIB_DIR" = "${rocksdb}/lib/";

          # Disable warnings that cause aws-lc-sys build to fail in release mode
          NIX_CFLAGS_COMPILE = "-Wno-error=stringop-overflow -Wno-error=array-bounds -Wno-error=restrict";
        };
      }
    );
}
